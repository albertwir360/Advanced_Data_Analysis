proc print data=sashelp.iris; 
run;
* use complete linkage and show last 15 merges;
proc cluster data=sashelp.iris method=complete outtree=iriscomplete print=15;
   var petal: sepal:;
   copy species;
   ods select ClusterHistory Dendrogram;
run;
* try 3 clusters because we know there are 3 species;
proc tree data=iriscomplete noprint ncl=3 out=out;
   copy petal: sepal: species;
run;
proc print data=out;
run;
* cross-tabulation to see how well clusters match up with species;
proc freq data=out;
  tables cluster*species/ nopercent norow nocol;
run;
* use single linkage and show last 15 merges;
proc cluster data=sashelp.iris method=single outtree=irissingle print=15;
   var petal: sepal:;
   copy species;
   ods select ClusterHistory Dendrogram;
run;
proc tree data=irissingle noprint ncl=3 out=outs;
   copy petal: sepal: species;
run;
proc print data=outs;
run;
proc freq data=outs;
  tables cluster*species/ nopercent norow nocol;
run;
* use average linkage and show last 15 merges;
proc cluster data=sashelp.iris method=average outtree=irisaverage print=15;
   var petal: sepal:;
   copy species;
   ods select ClusterHistory Dendrogram;
run;
proc tree data=irisaverage noprint ncl=3 out=outa;
   copy petal: sepal: species;
run;
proc print data=outa;
run;
proc freq data=outa;
  tables cluster*species/ nopercent norow nocol;
run;
* get ccc and pseudo t^2 and F stats and diagnostic plots for average linkage case;
proc cluster data=sashelp.iris method=average ccc pseudo print=15 plots=all;
	var petal: sepal:;
   	copy species;
	ods select ClusterHistory Dendrogram CccPsfAndPsTSqPlot;
run;
* U.S. air quality data set;
data usair;
  infile '' expandtabs;
  input city $16. so2 temperature factories population windspeed rain rainydays;
run;
proc print data=usair;
run;
* use univariate analysis to identify extreme observations;
proc univariate data=usair;
  var temperature--rainydays;
  id city;
  ods select ExtremeObs;
run;
*remove Chicago and Phoenix which are deemed extreme on several variables;
data usair2;
  set usair;
  if city not in('Chicago','Phoenix');
run;
* variables are on very different scales, so standardize measurements;
* use complete linkage and obtain ccc values for number of clusters;
proc cluster data=usair2 method=complete ccc std outtree=complete;
  var temperature--rainydays;
  id city;
  copy so2;
  ods select ClusterHistory Dendrogram CubicClusCritPlot;
run;
* number of clusters is not obvious from ccc diagnostics, 
  but we choose 4 to continue on with analysis as an illustrative example;
* obtain the 4 clusters for analysis and retain original variables for later analyses;
proc tree data=complete out=clusters n=4 noprint;
  copy city so2 temperature--rainydays;
run;
* sort by cluster so we can do analysis by cluster;
proc sort data=clusters;
 by cluster;
run;
proc print data=clusters;
run;
* do means analysis on variables by cluster;
proc means data=clusters;
 var temperature--rainydays;
 by cluster;
run;
* perform principal components analysis on cluster data to extract 2 most prominent features;
proc princomp data=clusters n=2 out=pcout;
  var temperature--rainydays;
run;
* visualize the data points in the first two principal 
  coordinates and see where the clustered values are in this space;
proc sgplot data=pcout;
  scatter y=prin1 x=prin2 / markerchar=cluster;
run;
* visualize the distribution of SO2 values by cluster;
proc sgplot data=clusters;
  vbox so2 / category=cluster;
run;
* perform an analysis of variance on the SO2 levels as a function of cluster;
proc anova data=clusters;
  class cluster;
  model so2=cluster;
  means cluster/ hovtest cldiff tukey;
  ods select OverallANOVA CLDiffs HOVFTest;
run;
* nonparametric tests to avoid normality assumption;
* use the Dwass, Steel, Critchlow-Fligner multiple comparison procedure for multiple comparisons in nonparametric 1-way ANOVA;
proc npar1way data=clusters wilcoxon DSCF;
	class cluster;
  	var so2;
run;

