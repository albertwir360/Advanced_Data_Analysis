*back to the water data...;
data water;
    infile '/folders/myfolders/STAT448 Final/Data/HW2/WATER.DAT';
    input flag $ 1 Town $ 2-18 Mortal 19-22 Hardness 25-27;
    if flag='*' then location='north';
        else location='south';
run;
* get counts and proportions via proc freq;
proc freq data=water;
   tables location;
run;
* obtain bar charts via proc gchart;
* Use sgplot instead of gchart, since gchart does not work in SAS University Edition;
proc sgplot data=water;
   hbar location;
run;
* Change bar orientation;
proc sgplot data=water;
   vbar location;
run;
* add grouping variables that indicate whether mortality rates are greater than 
  sample median and whether calcium concentrations are greater than sample median;
data water;
    set water;
    mortgrp=mortal > 1555;
    hardgrp=hardness > 39;
run;
* see the added variables;
proc print data=water;
run;
* cross-tabulate the groupings and leave out percentages that are generated by default;
proc freq data=water;
  tables mortgrp*hardgrp/ nopercent norow nocol;
run;
* see the expected counts for each cell;
proc freq data=water;
  tables mortgrp*hardgrp/ nopercent norow nocol expected;
run;
* obtain results based on chi-square statistics and Fisher's exact test;
proc freq data=water;
  tables mortgrp*hardgrp/ nopercent norow nocol expected chisq;
run;
* see contributions from each cell to the Pearson chi-square;
proc freq data=water;
  tables mortgrp*hardgrp/ nopercent norow nocol cellchi2 chisq;
run;
* define sandflies data set;
data sandflies;
    input sex $ height n;
cards;
m 3  173
m 35 125
f 3  150
f 35  73
;
run;
proc print data=sandflies;
run;
* specify counts as weight and get frequencies and expected frequencies;
proc freq data=sandflies;
  tables sex*height/ nocol norow nopercent expected chisq;
  weight n;
run;
* look at risks and risk differences for mortality groups and calcium concentration groups;
* include row percentages;
proc freq data=water;
  tables mortgrp*hardgrp/ nopercent nocol riskdiff;
run;
* flip rows and columns to get other risks;
proc freq data=water;
  tables hardgrp*mortgrp/ nopercent nocol riskdiff;
run;
* risks and risk differences for sandflies data;
proc freq data=sandflies;
	tables sex*height/ nopercent nocol riskdiff;
	weight n;
run;
proc freq data=sandflies;
	tables height*sex/ nopercent nocol riskdiff;
	weight n;
run;
* acacia ant data-- note code from textbook's files incorrectly has two counts for B no;
data ants;
  input species $ invaded $ n;
cards;
A no 2
A yes 13
B no 10 
B yes 3
;
run;
* results for risks of species to be invaded by ants;
proc freq data=ants;
	tables species*invaded/ nopercent nocol expected chisq riskdiff;
	weight n;
run;
* oral cancer data;
data lesions;
  length region $8.;
  input site $ 1-16 n1 n2 n3;
  region='Keral';
  n=n1;
  output;
  region='Gujarat';
  n=n2;
  output;
  region='Anhara';
  n=n3;
  output;
  drop n1-n3;
cards;
Buccal Mucosa    8  1  8
Labial Mucosa    0  1  0
Commissure       0  1  0
Gingiva          0  1  0
Hard palate      0  1  0
Soft palate      0  1  0
Tongue           0  1  0
Floor of mouth   1  0  1
Alveolar ridge   1  0  1
;
run;
proc print data=lesions;
run;
* results to test for association;
proc freq data=lesions;
	tables region*site/nopercent norow nocol expected chisq exact;
	weight n;
run;
